MODULE PayslipCategory;

REQUIRE HumanResourcesSettings, MasterData;

PRIORITY HumanResources;

NAMESPACE Payroll;

CLASS PayslipCategory 'Payslip category';
TABLE payslipCategory (PayslipCategory);

name '{master.data.name}' = DATA ISTRING[100] (PayslipCategory) CHARWIDTH 15;
deduction 'Deduction' = DATA BOOLEAN (PayslipCategory);

editable 'Editable' = DATA BOOLEAN (PayslipCategory);
readonly (PayslipCategory p) = p IS PayslipCategory AND NOT editable(p);

FORM payslipCategory 'Payslip category'
    OBJECTS c = PayslipCategory PANEL
    PROPERTIES(c) name, deduction, editable
    
    EDIT PayslipCategory OBJECT c
;

FORM categories 'Payslip categories'
    OBJECTS c = PayslipCategory
    PROPERTIES(c) READONLY name, deduction

    LIST PayslipCategory OBJECT c
;

EXTEND FORM options
    OBJECTS pc = PayslipCategory
    PROPERTIES(pc) READONLY name, deduction, editable
    PROPERTIES(pc) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(pc) { caption = 'Payslip categories'; }
    }
}