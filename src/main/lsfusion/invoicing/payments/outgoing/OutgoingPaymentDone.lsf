MODULE OutgoingPaymentDone;

REQUIRE OutgoingPaymentReady;

NAMESPACE Invoicing;

EXTEND CLASS OutgoingPaymentStatus {
    done 'Done'
}
done 'Done' = DATA BOOLEAN (OutgoingPayment);

status(OutgoingPayment s) += WHEN done(s) THEN OutgoingPaymentStatus.done;

markAsDone 'Mark as Done' (OutgoingPayment s) {
    APPLY;
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        done(s) <- TRUE;
        APPLY;
    }
}

EXTEND FORM outgoingPayment
    PROPERTIES(p) markAsDoneReady = markAsDone SHOWIF status(p) = OutgoingPaymentStatus.ready,
                  markAsDoneDraft = markAsDone SHOWIF status(p) = OutgoingPaymentStatus.draft,
                  done
;

DESIGN outgoingPayment {
    primaryActions {
        MOVE PROPERTY(markAsDoneReady) { valueClass = 'btn-primary'; };
    }
    secondaryActions {
        MOVE PROPERTY(markAsDoneDraft) { valueClass = 'btn-secondary'; }
    }
    statusGroup {
        MOVE PROPERTY(done(p)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }
    }
}

EXTEND FORM outgoingPayments
    EXTEND FILTERGROUP status
        FILTER 'Done' status(p) = OutgoingPaymentStatus.done
;
