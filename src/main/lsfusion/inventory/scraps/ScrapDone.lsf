MODULE ScrapDone;

REQUIRE Scrap;

NAMESPACE Inventory;

EXTEND CLASS ScrapStatus {
    done 'Done'
}
done 'Done' = DATA BOOLEAN (Scrap);

status(Scrap s) += WHEN done(s) THEN ScrapStatus.done;

markAsDone 'Mark as Done' (Scrap s) {
    APPLY;
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        done(s) <- TRUE;
        APPLY;
    }
}

EXTEND FORM scrap
    PROPERTIES(s) markAsDone SHOWIF status(s) = ScrapStatus.draft, done
;

DESIGN scrap {
    primaryActions {
        MOVE PROPERTY(markAsDone(s)) { valueClass = 'btn-primary'; };
    }
    statusGroup {
        MOVE PROPERTY(done(s)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }
    }
}

EXTEND FORM scraps
    EXTEND FILTERGROUP status
        FILTER 'Done' status(s) = ScrapStatus.done
;

DESIGN scraps {
    filters {
        MOVE FILTERGROUP(status);
    }
}