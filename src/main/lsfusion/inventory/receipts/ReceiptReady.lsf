MODULE ReceiptReady;

REQUIRE Receipt, ResLedger, ReceiptSelection;

NAMESPACE Inventory;

EXTEND CLASS ReceiptStatus {
    ready 'Ready'
}
ready 'Ready' = DATA BOOLEAN (Receipt);

status (Receipt r) += WHEN ready(r) THEN ReceiptStatus.ready;
color (ReceiptStatus r) += WHEN r = ReceiptStatus.ready THEN RGB(252,247,149);

markAsToDo 'Mark as Todo' (Receipt r) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        ready(r) <- TRUE;
        APPLY;
    }
}

EXTEND FORM receipt
    PROPERTIES(r) markAsToDo SHOWIF status(r) = ReceiptStatus.draft AND NOT immediate(r), ready 
;

DESIGN receipt {
    primaryActions {
        MOVE PROPERTY(markAsToDo(r)) { valueClass = 'btn-primary'; }
    }       
    statusGroup {
        MOVE PROPERTY(ready(r)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }                               
    }
}

EXTEND FORM receipts    
    EXTEND FILTERGROUP status
        FILTER 'Ready' status(r) = ReceiptStatus.ready    
;

@defineSelectionAction(receipts, Receipt, markAsToDo, 'Mark as Todo', statuses);