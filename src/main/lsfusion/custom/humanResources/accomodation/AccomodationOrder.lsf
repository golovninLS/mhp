MODULE AccomodationOrder;

REQUIRE HumanResourcesSettings,
        Employee, Accomodation;

PRIORITY MasterData;

NAMESPACE Accomodation;

CLASS Order 'Accomodation order';

startDate 'Start date' = DATA DATE (Order) NONULL;
endDate 'End date' = DATA DATE (Order);

employee 'Employee' = DATA Employee (Order) NONULL;
nameEmployee 'Employee' (Order o) = name(employee(o));

accomodation 'Accomodation' = DATA Accomodation (Order) NONULL;
nameAccomodation 'Accomodation' (Order o) = name(accomodation(o));

order (DATE d, Employee e) = GROUP MIN Order o IF startDate(o) <= d AND NOT d > endDate(o) AND employee(o) = e;

prevOrder (DATE d, Employee e) = GROUP LAST Order o ORDER startDate(o), o WHERE startDate(o) < d AND employee(o) = e;
nextOrder (DATE d, Employee e) = GROUP LAST Order o ORDER DESC startDate(o), o WHERE startDate(o) > d AND employee(o) = e;

countEmployees (Accomodation a, DATE d) = GROUP SUM 1 IF accomodation(order(d, Employee e)) = a;
employeePrice (Accomodation a, DATE d) = round2(price(a, d) / (IF fixedPrice(a, d) THEN countEmployees(a, d) ELSE 1));

accomodationOrder (DATE d, Employee e) = accomodation(order(d, e));
nameAccomodationOrder 'Accomodation' (DATE d, Employee e) = name(accomodationOrder(d, e));
symbolAccomodationOrder 'Accomodation' (DATE d, Employee e) = symbol(accomodationOrder(d, e));
rgbColorAccomodationOrder (DATE d, Employee e) = rgbColor(accomodationOrder(d, e));
employeePriceAccomodationOrder 'Price' (DATE d, Employee e) = employeePrice(accomodationOrder(d, e), d);
countEmployeesAccomodationOrder 'Employees' (DATE d, Employee e) = countEmployees(accomodationOrder(d, e), d);

accomodation 'Accomodation order' (Employee e, DATE from, DATE to) =
    GROUP SUM employeePriceAccomodationOrder(DATE d, e) IF iterate(d, from, to);
accomodation 'Accomodation order' (Employee e, INTERVAL[DATE] i) = accomodation(e, from(i), to(i));

accomodationDays 'Accomodation order (days)' (Employee e, DATE from, DATE to) =
    GROUP SUM 1 IF accomodationOrder(DATE d, e) AND iterate(d, from, to);
accomodationDays 'Accomodation order (days)' (Employee e, INTERVAL[DATE] i) = accomodationDays(e, from(i), to(i));

FORM order 'Accomodation order'
    OBJECTS o = Order PANEL
    PROPERTIES(o) startDate, endDate, nameEmployee, nameAccomodation
    
    EDIT Order OBJECT o
;

DESIGN order {
    OBJECTS {
        NEW dates {
            horizontal = TRUE;
            MOVE PROPERTY(startDate(o));
            MOVE PROPERTY(endDate(o));
        }
        MOVE PROPERTY(nameEmployee(o)) { alignment = STRETCH; }
        MOVE PROPERTY(nameAccomodation(o)) { alignment = STRETCH; }
    }
}

FORM orders 'Accomodation orders'
    OBJECTS o = Order
    PROPERTIES(o) READONLY startDate, endDate, nameEmployee, nameAccomodation
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
;

FORM ordersDateEmployee 'Accomodation orders'
    OBJECTS i = INTERVAL[DATE] PANEL
    OBJECTS e = Employee PANEL

    OBJECTS d = DATE
    PROPERTIES READONLY BACKGROUND rgbColorAccomodationOrder(d, e) 'Date' = VALUE(d), nameAccomodationOrder(d, e), employeePriceAccomodationOrder(d, e), countEmployeesAccomodationOrder(d, e)
    FILTERS iterate(d, from(i), to(i))
;

NAVIGATOR {
    operations {
        NEW orders;
    }
}