MODULE AccomodationOrder;

REQUIRE HumanResourcesSettings,
        Employee, Accomodation;

PRIORITY MasterData;

NAMESPACE Accomodation;

CLASS Order 'Accomodation order';

startDate 'Start date' = DATA DATE (Order) NONULL;
endDate 'End date' = DATA DATE (Order);

employee 'Employee' = DATA Employee (Order);
nameEmployee 'Employee' (Order o) = name(employee(o));

accomodation 'Accomodation' = DATA Accomodation (Order);
nameAccomodation 'Accomodation' (Order o) = name(accomodation(o));

order (DATE d, Employee e) = GROUP MIN Order o IF startDate(o) <= d AND NOT d > endDate(o) AND employee(o) = e;

prevOrder (DATE d, Employee e) = GROUP LAST Order o ORDER startDate(o), o WHERE startDate(o) < d AND employee(o) = e;
nextOrder (DATE d, Employee e) = GROUP LAST Order o ORDER DESC startDate(o), o WHERE startDate(o) > d AND employee(o) = e;

accomodationOrder (DATE d, Employee e) = accomodation(order(d, e));
symbolAccomodationOrder 'Accomodation order' (DATE d, Employee e) = symbol(accomodationOrder(d, e));
rgbColorAccomodationOrder (DATE d, Employee e) = rgbColor(accomodationOrder(d, e));

accomodation 'Accomodation order' (Employee e, DATE from, DATE to) =
    GROUP SUM price(accomodationOrder(DATE d, e), d) IF iterate(d, from, to);
accomodation 'Accomodation order' (Employee e, INTERVAL[DATE] i) = accomodation(e, from(i), to(i));

accomodationDays 'Accomodation order (days)' (Employee e, DATE from, DATE to) =
    GROUP SUM 1 IF accomodationOrder(DATE d, e) AND iterate(d, from, to);
accomodationDays 'Accomodation order (days)' (Employee e, INTERVAL[DATE] i) = accomodationDays(e, from(i), to(i));

FORM order 'Accomodation order'
    OBJECTS o = Order PANEL
    PROPERTIES(o) startDate, endDate, nameEmployee, nameAccomodation
    
    EDIT Order OBJECT o
;

FORM orders 'Accomodation orders'
    OBJECTS o = Order
    PROPERTIES(o) READONLY startDate, endDate, nameEmployee, nameAccomodation
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
;

NAVIGATOR {
    operations {
        NEW orders;
    }
}