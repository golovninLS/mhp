MODULE ContractCustom;

REQUIRE  Contract, ContractType, ContractStatus, Employee;

NAMESPACE MasterData;

createDate 'Дата создания' = DATA DATE (Contract) IN id;
createDate(Contract c) <- currentDate() WHEN SET(c IS Contract);

creator 'Автор' = DATA Employee (Contract) IN id;
WHEN SET(Contract c IS Contract) AND NOT creator(c) AND currentUser() IS Employee DO creator(c) <- currentUser();
idCreator 'Код автора' (Contract p) = id(creator(p));
nameCreator 'Автор' (Contract p) = name(creator(p)) IN id;

contractType 'Тип договора' = DATA ContractType (Contract) NONULL;
idContractType 'Код типа договора' (Contract p) = id(contractType(p));
nameContractType 'Тип договора' (Contract p) = name(contractType(p)) IN id;

contractStatus 'Статус договора' = DATA ContractStatus (Contract) NONULL;
idContractStatus 'Код статуса договора' (Contract p) = id(contractStatus(p));
nameContractStatus 'Статус договора' (Contract p) = name(contractStatus(p)) IN id;

EXTEND FORM contract PROPERTIES (c) PANEL nameContractType, nameContractStatus, nameCreator, createDate;

DESIGN contract {
    header{
        headerLeft{
            NEW creation AFTER legal{
                caption = 'Данные о создании';
                horizontal = TRUE;
                alignment = STRETCH;
                MOVE PROPERTY(nameCreator(c));
                MOVE PROPERTY(createDate(c));
            }
        }
    }
    
}

EXTEND FORM contracts PROPERTIES (c) READONLY nameContractType, nameContractStatus, nameCreator, createDate;

