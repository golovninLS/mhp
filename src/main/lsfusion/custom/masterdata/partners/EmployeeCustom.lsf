MODULE EmployeeCustom;

REQUIRE Employee;

NAMESPACE MasterData;

background = ABSTRACT CASE COLOR (Employee);
comment 'Comment' = DATA STRING (Employee);

FORM employeesCustom 'Employees'
    OBJECTS e = Employee BACKGROUND background(e)
    PROPERTIES(e) READONLY firstName, lastName, name, login, namePosition, nameLegalEntity
    PROPERTIES (e) comment
    PROPERTIES(e) NEWSESSION NEW, EDIT, DELETE

    LIST Employee OBJECT e
;

NAVIGATOR {
    masterData {
        NEW employeesCustom AFTER companies;
    }
}

EXTEND FORM employeesCustom
    FILTERGROUP filters
        FILTER '{Active}' NOT archived(e) DEFAULT
;

EXTEND FORM employeesCustom
    EXTEND FILTERGROUP filters
        FILTER '{Inactive}' archived(e)
;

showAllCompaniesData 'Показывать все компании' = DATA BOOLEAN (CustomUser);

EXTEND FORM customUsers
    PROPERTIES showAllCompaniesData(u)
;

accessToCompany = DATA BOOLEAN (CustomUser, Company);
in 'Incl.' (CustomUser u, Company c) = OVERRIDE accessToCompany(u, c), TRUE IF legalEntity(u) = c;

EXTEND FORM employee
    OBJECTS comp = Company
    PROPERTIES(comp) READONLY name
    PROPERTIES(e, comp) in
;

DESIGN employee {
    details {
        NEW companies LAST {
            showIf = showAllCompaniesData(currentUser());
            caption = 'Company';
            MOVE BOX(comp) { caption = ''; };
        }
    }
}

showInfo 'Отображать вкладку инфо' = DATA BOOLEAN (UserRole);
showRoles 'Отображать вкладку роли' = DATA BOOLEAN (UserRole);
showLocations 'Отображать вкладку места хранения' = DATA BOOLEAN (UserRole);
showInventory 'Отображать вкладку инвентарь сотрудника' = DATA BOOLEAN (UserRole);
showNotifications 'Отображать вкладку инвентарь уведомления' = DATA BOOLEAN (UserRole);
showCompanies 'Отображать вкладку компания' = DATA BOOLEAN (UserRole);

EXTEND FORM securityPolicy PROPERTIES(ur) PANEL showInfo, showRoles, showLocations, showInventory, showCompanies;
DESIGN securityPolicy {
    rolePolicyContainer {
        NEW permissionsContainer {
            caption = 'Настройки приложения';
            MOVE PROPERTY(showInfo(ur));
            MOVE PROPERTY(showRoles(ur));
            MOVE PROPERTY(showLocations(ur));
            MOVE PROPERTY(showInventory(ur));
            MOVE PROPERTY(showCompanies(ur));
        }
    }
}