MODULE EmployeeCustom;

REQUIRE Employee, CountryP;

NAMESPACE MasterData;

background = ABSTRACT CASE COLOR (Employee);
comment 'Comment' = DATA STRING (Employee);

FORM employeesCustom 'Employees'
    OBJECTS e = Employee BACKGROUND background(e)
    PROPERTIES(e) READONLY firstName, lastName, name, login, namePosition, nameLegalEntity
    PROPERTIES (e) comment
    PROPERTIES(e) NEWSESSION NEW, EDIT, DELETE

    LIST Employee OBJECT e
;

NAVIGATOR {
    masterData {
        NEW employeesCustom AFTER companies;
    }
}

EXTEND FORM employeesCustom
    FILTERGROUP filters
        FILTER '{Active}' NOT archived(e) DEFAULT
;

EXTEND FORM employeesCustom
    EXTEND FILTERGROUP filters
        FILTER '{Inactive}' archived(e)
;


accessToCompany = DATA BOOLEAN (CustomUser, Company);
in 'Incl.' (CustomUser u, Company c) = OVERRIDE accessToCompany(u, c), TRUE IF legalEntity(u) = c;

EXTEND FORM employee
    OBJECTS comp = Company
    PROPERTIES(comp) READONLY name
    PROPERTIES(e, comp) in
;


showInfo 'Отображать вкладку инфо' = DATA BOOLEAN (UserRole);
showRoles 'Отображать вкладку роли' = DATA BOOLEAN (UserRole);
showLocations 'Отображать вкладку места хранения' = DATA BOOLEAN (UserRole);
showInventory 'Отображать вкладку инвентарь сотрудника' = DATA BOOLEAN (UserRole);
showNotifications 'Отображать вкладку уведомления' = DATA BOOLEAN (UserRole);
showCompanies 'Отображать вкладку компания' = DATA BOOLEAN (UserRole);

EXTEND FORM securityPolicy PROPERTIES(ur) PANEL showInfo, showRoles, showLocations, showInventory, showCompanies, showNotifications;
DESIGN securityPolicy {
    rolePolicyContainer {
        NEW permissionsContainer {
            caption = 'Настройки приложения';
            horizontal = TRUE;
            NEW block1 {
                MOVE PROPERTY(showInfo(ur));
                MOVE PROPERTY(showRoles(ur));
                MOVE PROPERTY(showLocations(ur));
                MOVE PROPERTY(showInventory(ur));
                MOVE PROPERTY(showCompanies(ur));
                MOVE PROPERTY(showNotifications(ur));
            }
        }
    }
}

canPayBills 'Оплата фактур' = DATA BOOLEAN (UserRole);

showAllProjects 'Отображать все проекты' = DATA BOOLEAN (UserRole);
showAllCompaniesData 'Показывать все компании' = DATA BOOLEAN (UserRole);

EXTEND FORM securityPolicy PROPERTIES (ur) PANEL canPayBills, showAllProjects, showAllCompaniesData;
DESIGN securityPolicy {
    permissionsContainer {
        NEW access FIRST {
            MOVE PROPERTY(showAllProjects(ur));
            MOVE PROPERTY(showAllCompaniesData(ur));
        }
        NEW block2 {
            MOVE PROPERTY(canPayBills(ur));
        }
    }
}

DESIGN employee {
    details {
        NEW companies LAST {
            showIf = GROUP SUM 1 IF has(currentUser(), UserRole urr) AND showAllCompaniesData(urr);
            caption = 'Company';
            MOVE BOX(comp) { caption = ''; };
        }
    }
}

employeeCountry = DATA Country (Employee);
nameEmployeeCountry 'Страна' (Employee e) = name(employeeCountry(e));

EXTEND FORM employee PROPERTIES (e) nameEmployeeCountry;

DESIGN employee { 
    info {
        MOVE PROPERTY (nameEmployeeCountry(e)) AFTER PROPERTY (state(e));
    }
}