MODULE TelegramNotification;

REQUIRE TelegramCustom, EmployeeCustom, Notification;

NAMESPACE Integration;

employee = DATA Employee (Chat);
nameEmployee 'Employee' (Chat c) = name[Partner](employee(c));
chat = GROUP AGGR Chat c BY employee(c);
nameChat 'Chat' (Employee e) = name(chat(e));

EXTEND FORM employeesCustom
    PROPERTIES (e) READONLY nameChat
    FILTERGROUP telegram
        FILTER 'Telegram' chat(e)
;

EXTEND FORM messengers
    PROPERTIES nameEmployee(c)
;

NAVIGATOR {
    masterData {
        MOVE messengers;
    }
}

captionAttachment (Chat c) = IF newAttachment(c) THEN name(newAttachment(c)) ELSE 'Attach file';

attachment 'Attach' (Chat c){
    INPUT f = NAMEDFILE DO newAttachment(c) <- f;
}

in 'Уведомлять по telegram' = DATA BOOLEAN (Chat, NotificationType) CHARWIDTH 12;

EXTEND FORM employee
    OBJECTS c = Chat PANEL
    PROPERTIES in(c, nt) DRAW nt FIRST
    PROPERTIES(c) PANEL newMessage, sendMessage, attachment HEADER captionAttachment(c)
    FILTERS c = chat(e)
;

DESIGN employee {
    details {
        notifications {
            showIf = GROUP SUM 1 IF has(currentUser(), UserRole role) AND showNotifications(role);
            MOVE BOX (nt);
            NEW message {
                caption = 'Message';
                horizontal = TRUE;
                alignment = STRETCH;
                MOVE PROPERTY (newMessage(c));
                NEW action {
                    MOVE PROPERTY (attachment(c));
                    MOVE PROPERTY (sendMessage(c));
                }
            }
        }
    }
}

notifyUser (NotificationType type, STRING message) {
    timeoutHttp() <- 20000;
    FOR in(Chat c, type) DO TRY sendMessage(c, message);
}