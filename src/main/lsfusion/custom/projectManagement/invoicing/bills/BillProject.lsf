MODULE BillProject;

REQUIRE BillCurrency, ProjectBill, SalesAccount;

NAMESPACE ProjectManagement;

CLASS BillProject 'Project bill';

bill 'Bill' = DATA Bill (BillProject) NONULL DELETE;

countBillProject (Bill b) = GROUP SUM 1 IF bill(BillProject p) = b;
minBillProject (Bill b) = GROUP MIN BillProject p IF bill(p) = b;

project 'Project' = DATA Project (BillProject);
nameProject 'Project' (BillProject b) = name(project(b));

salesAccount 'Sales account' = DATA CustomSalesAccount (BillProject);
nameSalesAccount 'Sales account' (BillProject p) = name(salesAccount(p));
monthAccount = DATA Month (BillProject);
numberMonthAccount 'Month' (BillProject b) = number(monthAccount(b));
yearAccount 'Year' = DATA INTEGER (BillProject) PATTERN '0000';
dateAccount (BillProject p) = OVERRIDE lastDayOfMonth(DATE('01.' + numberMonthAccount(p) + '.' + yearAccount(p))), date(bill(p));

base 'Distribution base' = DATA NUMERIC[10,2] (BillProject);

untaxedAmount '{Amount}'  = DATA NUMERIC[14,2] (BillProject);

taxAmount 'Taxes' (Tax t, BillProject p) = DATA NUMERIC[14,2] (Tax, BillProject);
taxAmount 'Taxes' (BillProject p) = GROUP SUM taxAmount(Tax t, p);

amount 'Total amount' (BillProject p) = untaxedAmount(p) (+) taxAmount(p);

file1(BillProject o) = file1(bill(o));
file2(BillProject o) = file2(bill(o));
file3(BillProject o) = file3(bill(o));
openFile1 'File 1' (BillProject o) { openFile(bill(o), 1); }
openFile2 'File 2' (BillProject o) { openFile(bill(o), 2); }
openFile3 'File 3' (BillProject o) { openFile(bill(o), 3); }

defaultTax = GROUP MAX Tax t IF name(t) == 'VAT' MATERIALIZED;
taxAmount (BillLine l) = taxAmount(defaultTax(), l);

EXTEND FORM bill
    OBJECTS bpt = Tax
    
    OBJECTS bp = BillProject
    PROPERTIES(bp) nameProject, nameSalesAccount, numberMonthAccount, yearAccount, base, untaxedAmount
    PROPERTIES taxAmount(bpt, bp) COLUMNS (bpt) HEADER name(bpt)
    PROPERTIES(bp) amount READONLY
    PROPERTIES(bp) NEW, DELETE
    FILTERS bill(bp) = b
    
    PROPERTIES 'VAT' = taxAmount(l) ON CHANGE {
        INPUT n = NUMERIC[14,2] DO {
            in(l, defaultTax()) <- TRUE;
            taxAmount(defaultTax(), l) <- n;
        }
    }, 'BRUTTO' = amount(l)
;
WHEN SET (BillLine l IS BillLine) DO in(l, defaultTax()) <- TRUE;

DESIGN bill {
    details {
        NEW billProjects FIRST {
            caption = 'Projects';
            MOVE BOX(bp) {caption = ''; } 
        }
        linesFooter {
                caption = '';
                PROPERTY (untaxedAmount(l)) {caption = 'NETTO'; }
        }
    }
}

// bill properties
type (BillProject l) = type(bill(l));
nameType 'Type' (BillProject l) = nameType(bill(l)) IN id;
imagedNameStatus 'Status' (BillProject l) = imagedNameStatus(bill(l));
dateTime 'Date' (BillProject l) = dateTime(bill(l)) IN id;
date 'Date' (BillProject l) = date(bill(l));
dueDateTime 'Pay before' (BillProject l) = dueDateTime(bill(l)) IN id;
dueDate 'Pay before' (BillProject l) = dueDate(bill(l));
number 'Number' (BillProject l) = number(bill(l)) IN id;
numberDate 'Description' (BillProject l) = numberDate(bill(l));
vendor (BillProject l) = vendor(bill(l));
nameVendor 'Vendor' (BillProject l) = nameVendor(bill(l));
company 'Company' (BillProject l) = company(bill(l));
nameCompany 'Company' (BillProject l) = nameCompany(bill(l));
nameCurrency 'Currency' (BillProject l) = nameCurrency(bill(l));
items 'Items' (BillProject l) = items(bill(l));
note 'Note' (BillProject l) = note(bill(l));

@defineDateAggregation(BillProject, , );

customGenerate = ABSTRACT VALUE BOOLEAN (Bill);
generateProject 'Generate' ABSTRACT LIST (Bill);

// proportion

calcUntaxedAmount (BillProject p) =
    PARTITION UNGROUP untaxedAmount
        PROPORTION STRICT ROUND(2) base(p)
        ORDER p BY bill(p);

calcTaxAmount (Tax t, BillProject p) =
    PARTITION UNGROUP taxAmount
        PROPORTION STRICT ROUND(2) base(p)
        ORDER p BY t, bill(p);

generateProject(Bill b) + {
    IF NOT customGenerate(b) THEN {
        untaxedAmount(BillProject p) <- calcUntaxedAmount(p) WHERE bill(p) = b;
        taxAmount(Tax t, BillProject p) <- calcTaxAmount(t, p) WHERE bill(p) = b;
    }
}

EXTEND FORM bill
    PROPERTIES(b) generateProject DRAW bp TOOLBAR
;

WHEN LOCAL FORMS bill CHANGED(untaxedAmount(Bill b)) OR (GROUP SUM 1 IF CHANGED(taxAmount(Tax t, b))) OR
           (GROUP SUM 1 IF CHANGED(base(BillProject bp)) AND bill(bp) = b) DO
    generateProject(b);

WHEN LOCAL FORMS bill SET(Bill b IS Bill) AND NOT countBillProject(b) DO {
    NEW bp = BillProject {
        bill(bp) <- b;
        base(bp) <- 100;
    }
}

WHEN LOCAL FORMS bill CHANGED(project(Bill b)) DO
    project(minBillProject(b)) <- project(b);    

// sales account
//
//salesAccount (Bill b) = salesAccount(minBillProject(b));
//nameSalesAccount 'Sales account' (Bill b) = name(salesAccount(b));
//EXTEND FORM bill
//    PROPERTIES(b) nameSalesAccount ON CHANGE {
//        DIALOG customSalesAccounts OBJECTS o INPUT LIST name(o) DO {
//            salesAccount(minBillProject(b)) <- o;
//        }
//    }
//;
//
//DESIGN bill {
//    headerRight {
//        MOVE PROPERTY(nameSalesAccount(b));
//    }
//}