MODULE ProjectSalary;

REQUIRE ProjectTimeEntryPayslip, ProjectCustom;

PRIORITY MasterData;

NAMESPACE ProjectManagement;

CLASS ProjectSalary 'Project salary';

project 'Project' = DATA Project (ProjectSalary) NONULL;
countSalaries = GROUP SUM 1 BY project(ProjectSalary s) MATERIALIZED;

date 'Date' = DATA DATE (ProjectSalary) NONULL;
date (ProjectSalary t) <- currentDate() WHEN SET(t IS ProjectSalary);

dateTo 'Date to' = DATA DATE (ProjectSalary);

employee 'Employee' = DATA Employee (ProjectSalary) NONULL;
nameEmployee 'Employee' (ProjectSalary s) = name(employee(s));

rate 'Salary per hour' = DATA NUMERIC[10,2] (ProjectSalary) NONULL;

INDEX project(ProjectSalary s), employee(s), date(s), s;

salary (Project p, Employee e, DATE d) =
    GROUP LAST ProjectSalary s ORDER date(s), s WHERE date(s) <= d AND NOT dateTo(s) < d BY project(s), employee(s);

rateSalary 'Salary per hour' (Project p, Employee e, DATE d) = rate(salary(p, e, d));

rateSalary (TimeEntry e) += rateSalary(project(e), employee(e), date(e));

EXTEND FORM project
    OBJECTS ps = ProjectSalary
    PROPERTIES(ps) nameEmployee, date, dateTo, rate, NEW, DELETE
    FILTERS project(ps) = p
;

DESIGN project {
    staff {
        REMOVE BOX(ps);
    }
}
