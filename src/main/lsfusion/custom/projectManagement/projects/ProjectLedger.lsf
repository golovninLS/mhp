MODULE ProjectLedger;

REQUIRE Project, DateUtils;

NAMESPACE ProjectManagement;

CLASS ABSTRACT ProjectLedger 'Project ledger';
TABLE projectLedger (ProjectLedger);

active '{Active}' = ABSTRACT BOOLEAN (ProjectLedger);

class 'Type' (ProjectLedger l) = objectClassName(l) IF l IS ProjectLedger CHARWIDTH 15;

project 'Project' = ABSTRACT Project (ProjectLedger) MATERIALIZED;
nameProject 'Project' (ProjectLedger l) = name(project(l));

number 'Number' = ABSTRACT STRING[31] (ProjectLedger) MATERIALIZED;

date 'Date' = ABSTRACT DATE (ProjectLedger) MATERIALIZED;
@defineDateAggregation(ProjectLedger, , );

INDEX project (ProjectLedger l), date(l);

//// type
//CLASS ProjectLedgerType 'Project ledger type' {
//    none 'None'
//}
//TABLE projectLedgerType (ProjectLedgerType);
//
//name '{master.data.name}' (ProjectLedgerType o) = staticCaption(o) IF o IS ProjectLedgerType CHARWIDTH 15;
//
//FORM dialogProjectLedgerTypes 'Project ledger type'
//    OBJECTS o = ProjectLedgerType
//    PROPERTIES(o) READONLY name
//    
//    LIST ProjectLedgerType OBJECT o
//;
//
//type = ABSTRACT ProjectLedgerType (ProjectLedger) MATERIALIZED;
//nameType 'Type' (ProjectLedger l) = name(type(l));

partner 'Partner' = ABSTRACT Partner (ProjectLedger) MATERIALIZED;
namePartner 'Partner' (ProjectLedger l) = name(partner(l));

category 'Category' = ABSTRACT ISTRING (ProjectLedger) MATERIALIZED;

description 'Description' = ABSTRACT ISTRING (ProjectLedger) MATERIALIZED;

income 'Income' = ABSTRACT NUMERIC[16,2] (ProjectLedger) MATERIALIZED;
expense 'Expense' = ABSTRACT NUMERIC[16,2] (ProjectLedger) MATERIALIZED;
profit 'Profit' (ProjectLedger l) = income(l) (-) expense(l);

sumExpense 'Сумма затрат' (Project p, INTERVAL[DATE] i) = 
    GROUP SUM expense(ProjectLedger l) IF expense(l) > 0 AND date(l) >= from(i) AND date(l) <= to(i) BY project(l);
sumExpense 'Сумма затрат' (Project p) =
    GROUP SUM expense(ProjectLedger l) IF expense(l) > 0  BY project(l);
sumProfit 'Сумма прибыли' (Project p, INTERVAL[DATE] i) = 
    GROUP SUM profit(ProjectLedger l) IF profit(l) > 0 AND date(l) >= from(i) AND date(l) <= to(i) BY project(l);
sumProfit 'Сумма прибыли' (Project p) = 
    GROUP SUM profit(ProjectLedger l) IF profit(l) > 0 BY project(l);

EXTEND FORM project
    OBJECTS i = INTERVAL[DATE] PANEL NULL
    PROPERTIES i = VALUE(i)
    PROPERTIES (p, i) SHOWIF i IS INTERVAL[DATE] sumExpense, sumProfit
    PROPERTIES (p) SHOWIF NOT i IS INTERVAL[DATE] sumExpense, sumProfit
    
    OBJECTS prl = ProjectLedger
    PROPERTIES(prl) READONLY class, date, number, nameProject, namePartner, category, description, income, expense, profit
    PROPERTIES(prl) EDIT
    FILTERS project(prl) = p,
            active(prl),
            date(prl) >= from(i) AND date(prl) <= to(i) OR NOT i
;

DESIGN project {
    details {
        NEW projectLedger {
            fill = 1;
            caption = 'Project ledger';
            NEW dates {
                horizontal = TRUE;
                MOVE PROPERTY(i){caption = '';};
                MOVE PROPERTY(sumExpense(p, i));
                MOVE PROPERTY(sumExpense(p));
                MOVE PROPERTY(sumProfit(p, i));
                MOVE PROPERTY(sumProfit(p));
            }
            MOVE BOX(prl) {
                caption = '';
            }
        }
    }
}