MODULE ProjectCustom;

REQUIRE Project, TimesheetCustom, City, ProjectLedger;

NAMESPACE ProjectManagement;

managerTimesheetCloseDate 'Дата закрытия табеля (менеджер)' = DATA DATE (Project);
activeTimesheet(Project p) = (manager(p) = currentUser() AND startDate(p) AND NOT endDate(p) AND NOT managerTimesheetCloseDate(p) == supervisorTimesheetClosingDate()
    OR NOT supervisorTimesheetClosingDate()) AND p IS Project;

background(Project p) += WHEN activeTimesheet(p) AND supervisorTimesheetClosingDate() THEN RGB(224,224,255);

countActiveTimesheetProjects () = GROUP SUM 1 IF Project p IS Project AND activeTimesheet(p) AND supervisorTimesheetClosingDate();

city = DATA City (Project);
nameCity 'City' (Project a) = name(city(a));

latitude 'Latitude' (Project a) = latitude(city(a));
longitude 'Longitude' (Project a) = longitude(city(a));

CONSTRAINT (SETCHANGED(date(TimeEntry te)) OR SETCHANGED(project(te)) OR SETCHANGED(hours(te)) OR SETCHANGED(employee(te)) OR SETCHANGED(type(te)) OR SETCHANGED(description(te)) OR DROPPED(te IS TimeEntry)) AND 
    managerTimesheetCloseDate(project(te)) AND (NOT managerTimesheetCloseDate(project(te)) < date(te) OR NOT managerTimesheetCloseDate(project(te)) < PREV(date(te)))
    MESSAGE 'Нельзя изменять отметку времени в табеле на дату меньшую даты закрытия табеля(менеджер) проекта';

CONSTRAINT DROPPED(TimeEntry te IS TimeEntry) AND
    managerTimesheetCloseDate(PREV(project(te))) AND NOT managerTimesheetCloseDate(PREV(project(te))) < PREV(date(te))
    MESSAGE 'Нельзя изменять отметку времени в табеле на дату меньшую даты закрытия табеля(менеджер) проекта';

EXTEND FORM project
    PROPERTIES(p) managerTimesheetCloseDate, nameCity

    OBJECTS pp = Project MAP
    PROPERTIES(pp) name READONLY, latitude, longitude
    FILTERS pp = p
;

DESIGN project {
    caption = badged('Project', name(p));
    header {
        headerLeft {
            MOVE PROPERTY(nameCity(p));
        }
        headerRight {
            MOVE PROPERTY(managerTimesheetCloseDate(p));
        }
    }
    details {
        NEW staff {
            tabbed = TRUE;
            fill = 1;
            caption = 'Staff';            
        }
        
        MOVE BOX(pp) { caption = 'Map'; }
    }
}

EXTEND FORM projects
    PROPERTIES() READONLY supervisorTimesheetClosingDate PANEL
    PROPERTIES(p) READONLY managerTimesheetCloseDate, nameCity AFTER nameManager(p)
;

NAVIGATOR {
    operations {
        projects HEADER badged('Проекты', countActiveTimesheetProjects());;
    }
}