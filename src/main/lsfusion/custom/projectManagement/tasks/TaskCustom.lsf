MODULE TaskCustom;

REQUIRE TaskKanban, ProjectCustom, TelegramNotification;

NAMESPACE ProjectManagement;

defaultTaskStatus = DATA TaskStatus ();
nameDefaultStatus 'Ð¡Ñ‚Ð°Ñ‚ÑƒÑ Ð¿Ð¾ ÑƒÐ¼Ð¾Ð»Ñ‡Ð°Ð½Ð¸ÑŽ' = name(defaultTaskStatus());

EXTEND FORM options PROPERTIES nameDefaultStatus();
DESIGN options {
    tabbedPane {
        NEW statuses {
            fill = 1;
            alignment = STRETCH;
            caption = 'Task statuses';
            MOVE PROPERTY (nameDefaultStatus());
            MOVE BOX(ts);
        }
    }
}

showAllTasks 'ÐŸÐ¾ÐºÐ°Ð·Ñ‹Ð²Ð°Ñ‚ÑŒ Ð²ÑÐµ Ð·Ð°Ð´Ð°Ñ‡Ð¸' = DATA BOOLEAN (CustomUser);

EXTEND FORM tasks
    EXTEND FILTERGROUP my
        FILTER 'My tasks + Assigned to me' author(t) = currentUser() OR assignedTo(t) = currentUser() 'F7' DEFAULT 
    FILTERS (author(t) = currentUser() OR assignedTo(t) = currentUser()) OR showAllTasks(currentUser())
    FILTERS (author(tt) = currentUser() OR assignedTo(tt) = currentUser()) OR showAllTasks(currentUser())
;

EXTEND FORM customUsers
    PROPERTIES showAllTasks(u)
;

datePlannedTask 'Ð”Ð°Ñ‚Ð° ÑÐ¾Ð·Ð´Ð°Ð½Ð¸Ñ Ð·Ð°Ð´Ð°Ñ‡Ð¸ Ð¿Ð¾ Ð¿Ð»Ð°Ð½Ð¸Ñ€ÑƒÐµÐ¼Ð¾Ð¹ Ð²Ñ‹Ñ€ÑƒÑ‡ÐºÐµ' = DATA INTEGER ();
deadlinePlannedTask 'Ð”ÐµÐ´Ð»Ð°Ð¹Ð½ + Ð´Ð½ÐµÐ¹(Ð¿Ð»Ð°Ð½Ð¸Ñ€ÑƒÐµÐ¼Ð°Ñ Ð²Ñ‹Ñ€ÑƒÑ‡ÐºÐ°)' = DATA INTEGER ();

dateClosedTask 'Ð”Ð°Ñ‚Ð° ÑÐ¾Ð·Ð´Ð°Ð½Ð¸Ñ Ð·Ð°Ð´Ð°Ñ‡Ð¸ Ð¿Ð¾ Ð·Ð°ÐºÑ€Ñ‹Ñ‚Ð¸ÑŽ Ð¼ÐµÑÑÑ†Ð°' = DATA INTEGER ();
deadlineClosedTask 'Ð”ÐµÐ´Ð»Ð°Ð¹Ð½ + Ð´Ð½ÐµÐ¹(Ð·Ð°ÐºÑ€Ñ‹Ñ‚Ð¸Ðµ Ð¼ÐµÑÑÑ†Ð°)' = DATA INTEGER ();

createPeriodTasks 'Ð¡Ð¾Ð·Ð´Ð°Ñ‚ÑŒ Ð¿ÐµÑ€Ð¸Ð¾Ð´Ð¸Ñ‡ÐµÑÐºÐ¸Ðµ Ð·Ð°Ð´Ð°Ñ‡Ð¸' ABSTRACT LIST ();

createPeriodTasks() + {
    IF datePlannedTask() = extractDay(currentDate()) THEN {
        FOR Employee e = manager(Project p) AND (endDate(p) >= firstDayOfMonth(currentDate()) OR NOT endDate(p)) AND NOT
            planned(p, interval(firstDayOfMonth(currentDate()), lastDayOfMonth(currentDate())))
            DO NEWSESSION NEW t = Task{
            status(t) <- defaultTaskStatus();
            assignedTo(t) <- e;
            name(t) <- '[' + name(p) + '] Ð—Ð°Ð¿Ð¾Ð»Ð½ÐµÐ½Ð¸Ðµ Ð¿Ð»Ð°Ð½Ð¾Ð²Ð¾Ð¹ Ð²Ñ‹Ñ€ÑƒÑ‡ÐºÐ¸ Ð½Ð° ' + lower(name(extractMonth(currentDate())));
            deadline(t) <- sum(currentDate(), OVERRIDE deadlinePlannedTask(), 2);
            APPLY;
        }
    }
    IF dateClosedTask() = extractDay(currentDate()) THEN {
        FOR Employee e = manager(Project p) AND (endDate(p) >= sum(firstDayOfMonth(currentDate()), -1) OR NOT endDate(p)) DO NEWSESSION NEW t = Task {
            status(t) <- defaultTaskStatus();
            assignedTo(t) <- e;
            name(t) <- '[' + name(p) + '] Ð—Ð°ÐºÑ€Ñ‹Ñ‚Ð¸Ðµ Ñ‚Ð°Ð±ÐµÐ»Ñ Ð·Ð° ' + lower(name(extractMonth(sum(firstDayOfMonth(currentDate()), -1))));
            deadline(t) <- sum(currentDate(), OVERRIDE deadlineClosedTask(), 2);
            APPLY;
        }
    }
}

EXTEND FORM options
    PROPERTIES () datePlannedTask, deadlinePlannedTask, dateClosedTask, deadlineClosedTask, createPeriodTasks
;
DESIGN options{
    tabbedPane{
        NEW periodTasks{
            caption = 'ÐŸÐµÑ€Ð¸Ð¾Ð´Ð¸Ñ‡ÐµÑÐºÐ¸Ðµ Ð·Ð°Ð´Ð°Ñ‡Ð¸';
            MOVE PROPERTY (datePlannedTask());
            MOVE PROPERTY (deadlinePlannedTask());
            MOVE PROPERTY (dateClosedTask());
            MOVE PROPERTY (deadlineClosedTask());
            MOVE PROPERTY (createPeriodTasks());
        }
    }
}

EXTEND CLASS NotificationType {
    task 'Task'
}

taskTemplateInitialized = TRUE IF template(NotificationType.task);

onStarted() + {
    IF NOT taskTemplateInitialized() THEN {
        template(NotificationType.task) <-
        'Ð—Ð°Ð´Ð°Ñ‡Ð° Â«[name]Â» Ð² Ñ€Ð°Ð¼ÐºÐ°Ñ… Ð¿Ñ€Ð¾ÐµÐºÑ‚Ð° [nameProject] Ð½Ð°Ð·Ð½Ð°Ñ‡ÐµÐ½Ð° Ð½Ð° [nameAssignedTo].\nÐŸÑ€Ð¸Ð¾Ñ€Ð¸Ñ‚ÐµÑ‚: [namePriority].\nÐ¡Ñ€Ð¾Ðº Ð²Ñ‹Ð¿Ð¾Ð»Ð½ÐµÐ½Ð¸Ñ: [deadline].\nÐ¢ÐµÐºÑƒÑ‰Ð¸Ð¹ ÑÑ‚Ð°Ñ‚ÑƒÑ: [nameStatus].\nÐÐ²Ñ‚Ð¾Ñ€ Ð·Ð°Ð´Ð°Ñ‡Ð¸: [nameAuthor].';
        APPLY;
    }
}

messageNotification(Task t) =
    replace(replace(replace(replace(replace(replace(replace(template(NotificationType.task), '[name]', OVERRIDE name(t), ''),
        '[nameProject]', OVERRIDE nameProject(t), '-'),
        '[nameAuthor]', OVERRIDE nameAuthor(t), '-'),
        '[nameAssignedTo]', OVERRIDE nameAssignedTo(t), '-'),
        '[nameStatus]', OVERRIDE nameStatus(t), '-'),
        '[namePriority]', OVERRIDE namePriority(t), '-'),
        '[deadline]', OVERRIDE STRING(deadline(t)), '-');

WHEN (SETCHANGED(Task t IS Task) OR SETCHANGED(assignedTo(t))) AND chat(assignedTo(t)) DO {
    sendMessage(chat(assignedTo(t)), messageNotification(t));
}

WHEN SET(Task t IS Task) AND NOT status(t) DO {
    status(t) <- defaultTaskStatus();
}

CLASS CheckList 'Checklist';

task = DATA Task (CheckList);
name 'Name' = DATA STRING (CheckList) NONULL;
progressBar 'Progress' = ABSTRACT INTEGER (CheckList);

CLASS CheckListLine 'Checklist line';

checklist = DATA CheckList (CheckListLine);
description 'Description' = DATA STRING (CheckListLine);
deadline 'Deadline' = DATA DATE (CheckListLine);
responsible = DATA Employee (CheckListLine);
nameResponsible 'Responsible' (CheckListLine l) = name[Partner](responsible(l));
done 'Done' = DATA BOOLEAN (CheckListLine);

order 'â„–' = DATA INTEGER (CheckListLine);
WHEN LOCAL SETCHANGED(CheckListLine t IS CheckListLine) AND CheckList ct = checklist(t) AND NOT order(t) DO
    order(t) <- [GROUP MAX order(CheckListLine tt) BY checklist(tt)](ct) (+) 1;

amountLines (CheckList c) = GROUP SUM 1 IF checklist(CheckListLine cl) = c;
amountDone (CheckList c) = GROUP SUM 1 IF checklist(CheckListLine cl) = c AND done(cl);
progressBar (CheckList c) += round0((amountDone(c) * 100) / amountLines(c));

taskChecklists () = JSON FROM id = CheckList c, progressBar(c) WHERE c IS CheckList;

EXTEND FORM task
    OBJECTS c = CheckList
    PROPERTIES(c) name READONLYIF currentUser() != author(t) AND NOT showAllTasks(currentUser()) BACKGROUND RGB(163, 204, 164) IF progressBar(c) = 100
    //HEADER JSON FROM checklists = taskChecklists()
    PROPERTIES(c) SHOWIF currentUser() = author(t) OR showAllTasks(currentUser()) NEW, DELETE
    LIST CheckList OBJECT c
    FILTERS task(c) = t

    OBJECTS cl = CheckListLine
    PROPERTIES(cl) READONLYIF currentUser() != author(t) AND NOT showAllTasks(currentUser()) order READONLY, description, deadline, nameResponsible, done
    PROPERTIES(cl) SHOWIF (currentUser() = author(t) OR showAllTasks(currentUser())) AND c NEW, DELETE
    PROPERTIES(c) progressBar READONLY CUSTOM 'addProgressBar' DRAW cl PANEL
    ORDERS order(cl)
    FILTERS checklist(cl) = c
;

DESIGN task {
    details {
        NEW checklist {
            caption = 'Checklist';
            fill = 1;
            horizontal = TRUE;
            NEW headerL {
                fill = 1;
                MOVE BOX (c) { caption = ''; };
                PROPERTY (name(c)) { charWidth = 15; }
            }
            NEW bodyL {
                fill = 20;
                alignment = STRETCH;
                NEW box {
                    fill = 100;
                    MOVE BOX(cl) { caption = ''; };
                    PROPERTY (order(cl)) { charWidth = 3; }
                    PROPERTY (description(cl)) { charWidth = 40; }
                    PROPERTY (deadline(cl)) { charWidth = 15; }
                    PROPERTY (nameResponsible(cl)) { charWidth = 10; }
                    PROPERTY (done(cl)) { charWidth = 12; }
                }
                MOVE PROPERTY (progressBar(c)) { fill = 1; };
                MOVE TOOLBARBOX(cl);
            }
        }
    }
}

WHEN LOCAL FORMS task SETCHANGED(deadline(CheckListLine cl)) AND (deadline(cl) > deadline(task(checklist(cl))) OR NOT deadline(task(checklist(cl)))) DO
    deadline(task(checklist(cl))) <- deadline(cl);

CONSTRAINT CHANGED(author(Task t)) AND PREV(author(t)) != currentUser() AND NOT showAllTasks(currentUser())
    MESSAGE 'Ð¢Ð¾Ð»ÑŒÐºÐ¾ Ð°Ð²Ñ‚Ð¾Ñ€ Ð·Ð°Ð´Ð°Ñ‡Ð¸ Ð¼Ð¾Ð¶ÐµÑ‚ Ð½Ð°Ð·Ð½Ð°Ñ‡Ð¸Ñ‚ÑŒ Ð´Ñ€ÑƒÐ³Ð¾Ð³Ð¾ Ð°Ð²Ñ‚Ð¾Ñ€Ð°.';

onWebClientInit() + {
    onWebClientInit('checklist.js') <- 1;
    onWebClientInit('checklist.css') <- 2;
}

startDate 'Ð”Ð°Ñ‚Ð° Ð½Ð°Ñ‡Ð°Ð»Ð°' = ABSTRACT DATE (Task);

EXTEND FORM task PROPERTIES(t) startDate;

DESIGN task {
    params {
        MOVE PROPERTY(startDate(t)) BEFORE PROPERTY (deadline(t));
    }
}

EXTEND FORM tasks PROPERTIES(t) startDate BEFORE deadline(t);

statusNotReady (Task t) = TRUE IF (status(t) != taskStatus('done') AND status(t) != taskStatus('closed'));
nameTaskProject (Task t) = CONCAT ' â€” ', 'â€¢ ' + name(t), (OVERRIDE nameProject(t), 'Ð¿Ñ€Ð¾ÐµÐºÑ‚ Ð½Ðµ ÑƒÐºÐ°Ð·Ð°Ð½');
deadlineToChar (Task t) = toChar(deadline(t), 'dd.mm.yyyy');

//Ð½Ð°Ð·Ð½Ð°Ñ‡ÐµÐ½Ñ‹ Ð¼Ð½Ðµ
overdueTasks (Employee e) = 'ðŸ”´ ÐŸÑ€Ð¾ÑÑ€Ð¾Ñ‡ÐµÐ½Ð½Ñ‹Ðµ:\n' + (GROUP CONCAT (nameTaskProject(Task t) + ' â€” ' + deadlineToChar(t))
    IF assignedTo(t) = e AND deadline(t) < currentDate() AND statusNotReady(t), '\n' ORDER t);
deadlineTodayTasks (Employee e) = 'âš ï¸ Ð”ÐµÐ´Ð»Ð°Ð¹Ð½ ÑÐµÐ³Ð¾Ð´Ð½Ñ:\n' + (GROUP CONCAT nameTaskProject(Task t)
    IF assignedTo(t) = e AND deadline(t) = currentDate() AND statusNotReady(t), '\n' ORDER t);
assignedForMe (Employee e) = 'ÐÐ°Ð·Ð½Ð°Ñ‡ÐµÐ½Ñ‹ Ð¼Ð½Ðµ:\n' + CONCAT '\n', overdueTasks(e), deadlineTodayTasks(e);

//Ð¼Ð¾Ð¸ Ð·Ð°Ð´Ð°Ñ‡Ð¸
overdueTasksMine (Employee e) = 'ðŸ”´ ÐŸÑ€Ð¾ÑÑ€Ð¾Ñ‡ÐµÐ½Ð½Ñ‹Ðµ:\n' + (GROUP CONCAT (CONCAT ' â€” ', nameTaskProject (Task t), 'Ð¸ÑÐ¿Ð¾Ð»Ð½Ð¸Ñ‚ÐµÐ»ÑŒ', nameAssignedTo(t), deadlineToChar(t))
    IF author(t) = e AND deadline(t) < currentDate() AND statusNotReady(t), '\n' ORDER t);
deadlineTodayTasksMine (Employee e) = 'âš ï¸ Ð”ÐµÐ´Ð»Ð°Ð¹Ð½ ÑÐµÐ³Ð¾Ð´Ð½Ñ:\n' + (GROUP CONCAT (CONCAT ' â€” ', nameTaskProject (Task t), 'Ð¸ÑÐ¿Ð¾Ð»Ð½Ð¸Ñ‚ÐµÐ»ÑŒ', nameAssignedTo(t))
    IF author(t) = e AND deadline(t) = currentDate() AND statusNotReady(t), '\n' ORDER t);
createdByMe (Employee e) = 'ÐœÐ¾Ð¸ Ð·Ð°Ð´Ð°Ñ‡Ð¸:\n' + CONCAT '\n', overdueTasksMine(e), deadlineTodayTasksMine(e);

//Ð·Ð°Ð´Ð°Ñ‡Ð¸ Ð¿Ñ€Ð¾ÐµÐºÑ‚Ð°
overdueTasksProject (Employee e) = 'ðŸ”´ ÐŸÑ€Ð¾ÑÑ€Ð¾Ñ‡ÐµÐ½Ð½Ñ‹Ðµ:\n' + (GROUP CONCAT (CONCAT ' â€” ', nameTaskProject (Task t), deadlineToChar(t))
    IF in(project(t), e) AND NOT assignedTo(t) AND deadline(t) < currentDate() AND statusNotReady(t), '\n' ORDER t);
deadlineTodayTasksProject (Employee e) = 'âš ï¸ Ð”ÐµÐ´Ð»Ð°Ð¹Ð½ ÑÐµÐ³Ð¾Ð´Ð½Ñ:\n' + (GROUP CONCAT (CONCAT ' â€” ', nameTaskProject (Task t), deadlineToChar(t))
    IF in(project(t), e) AND NOT assignedTo(t) AND deadline(t) = currentDate() AND statusNotReady(t), '\n' ORDER t);
projectTasks (Employee e) = 'Ð—Ð°Ð´Ð°Ñ‡Ð¸ Ð¿Ñ€Ð¾ÐµÐºÑ‚Ð°:\n' + CONCAT '\n', overdueTasksProject(e), deadlineTodayTasksProject(e);

sendNotificationsAboutTasks () {
    timeoutHttp() <- 20000;
    FOR Chat c IS Chat AND employee(c) DO TRY {
        sendMessage(c, (CONCAT '\n\n', assignedForMe(employee(c)), createdByMe(employee(c)), projectTasks(employee(c))));
    } CATCH {
        MESSAGE 'Ð¡Ð¾Ð¾Ð±Ñ‰ÐµÐ½Ð¸Ðµ Ð½Ðµ Ð±Ñ‹Ð»Ð¾ Ð¾Ñ‚Ð¿Ñ€Ð°Ð²Ð»ÐµÐ½Ð¾ Ð´Ð»Ñ ' + nameEmployee(c);
        MESSAGE System.messageCaughtException();
    }
}