MODULE PurchaseOrderConfirmed;

REQUIRE PurchaseOrderSent;

NAMESPACE Purchase;

EXTEND CLASS OrderStatus {
    confirmed 'Confirmed'
}
confirmed 'Confirmed' = DATA BOOLEAN (Order);

status(Order o) += WHEN confirmed(o) THEN OrderStatus.confirmed;
color(OrderStatus o) += WHEN o = OrderStatus.confirmed THEN RGB(150,255,150);

confirm 'Confirm' (Order o){
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        confirmed(o) <- TRUE;
        APPLY;
    }
}

EXTEND FORM order 
     PROPERTIES(o) confirmDraft = confirm SHOWIF (status(o) = OrderStatus.draft),
                   confirmSent = confirm SHOWIF (status(o) = OrderStatus.sent),
                   confirmed
;

DESIGN order {
    primaryActions {
        MOVE PROPERTY(confirmSent) { valueClass = 'btn-primary'; };
        MOVE PROPERTY(confirmDraft) { valueClass = 'btn-primary'; }
    }
    statusGroup {
        MOVE PROPERTY(confirmed(o)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }
    }
}


EXTEND FORM orders    
    EXTEND FILTERGROUP status
        FILTER 'Confirmed' status(o) = OrderStatus.confirmed    
;

@defineSelectionAction(orders, Order, confirm, 'Confirm', statuses);